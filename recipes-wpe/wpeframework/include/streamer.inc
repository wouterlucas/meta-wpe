# Streamer supports QAM or AAMP implementation
# Add 'QAM' and/or 'Aamp' to STREAMER_IMPLEMENTATIONS
STREAMER_IMPLEMENTATIONS ?= "Aamp"

# AAMP variables
AAMP_STREAMER_FRONTENDS 	?= "0"
AAMP_STREAMER_WESTEROSSINK	?= "${@bb.utils.contains('DISTRO_FEATURES', 'wayland', 'ON', 'OFF', d)}"

STREAMER_EXTRA_FLAGS += "${@bb.utils.contains('STREAMER_IMPLEMENTATIONS', 'Aamp', '\
	-DPLUGIN_STREAMER_AAMP_FRONTENDS=${AAMP_STREAMER_FRONTENDS} \
	-DPLUGIN_STREAMER_AAMP_WESTEROSSINK=${AAMP_STREAMER_WESTEROSSINK} \
', '', d)}"

# QAM variables, adjust to your own need in your adaptation/local.conf
QAM_STREAMER_STANDARD 		?= "DVB"
QAM_STREAMER_FRONTENDS		?= "1"
QAM_STREAMER_DECODERS		?= "2"
QAM_STREAMER_ANNEX 			?= "B"
QAM_STREAMER_TS_SCANNING 	?= "OFF"
QAM_STREAMER_HOME_TS 		?= ""

STREAMER_EXTRA_FLAGS += "${@bb.utils.contains('STREAMER_IMPLEMENTATIONS', 'QAM', '\
	-DPLUGIN_STREAMER_BROADCAST_CABLE=ON \
	-DPLUGIN_STREAMER_BROADCAST_CABLE_ANNEX=${QAM_STREAMER_ANNEX} \
	-DPLUGIN_STREAMER_BROADCAST_STANDARD=${QAM_STREAMER_STANDARD} \
	-DPLUGIN_STREAMER_FRONTENDS=${QAM_STREAMER_FRONTENDS} \
	-DPLUGIN_STREAMER_DECODERS=${QAM_STREAMER_DECODERS} \
	-DPLUGIN_STREAMER_BROADCAST_TS_SCANNING=${QAM_STREAMER_TS_SCANNING} \
	-DPLUGIN_STREAMER_BROADCAST_HOME_TS=${QAM_STREAMER_HOME_TS} \
', '', d)}"


# Set dependencies
STREAMER_DEP ?= "${@bb.utils.contains('STREAMER_IMPLEMENTATIONS', 'Aamp', 'gst-plugins-rdk-aamp', '', d)}"

# Other options include:
# -DPLUGIN_STREAMER_BROADCAST_SATELLITE=ON
# -DPLUGIN_STREAMER_BROADCAST_TERRESTRIAL=ON
# Which are not yet supported by this recipe. Please see: https://github.com/WebPlatformForEmbedded/WPEFrameworkPlugins/blob/master/Streamer/CMakeLists.txt for more info

PACKAGECONFIG[streamer] = "-DPLUGIN_STREAMER=ON -DPLUGIN_STREAMER_IMPLEMENTATIONS=${STREAMER_IMPLEMENTATIONS} ${STREAMER_EXTRA_FLAGS},,${STREAMER_DEP}"
